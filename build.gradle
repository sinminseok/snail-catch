// 버전 푸쉬
// ./gradlew clean shadowJar publish
// git tag 1.0.3
// git push origin 1.0.3

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.snailcatch'
version = '1.0.3'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly platform("org.springframework.boot:spring-boot-dependencies:3.2.5")
    compileOnly 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.springframework:spring-context'
    compileOnly 'org.springframework:spring-web'

    api 'p6spy:p6spy:3.9.1'
    implementation 'com.github.vertical-blank:sql-formatter:2.0.3'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    enabled = false
}

shadowJar {
    archiveBaseName.set('snailcatch-sdk-fat')
    archiveVersion.set(version)
    archiveClassifier.set('')

    mergeServiceFiles()

    dependencies {
        exclude(dependency('org.springframework.boot:spring-boot-starter'))
        exclude(dependency('org.springframework:spring-context'))
        exclude(dependency('org.springframework:spring-web'))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'snailcatch-sdk'
            artifact(tasks.shadowJar)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sinminseok/snail-catch")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
