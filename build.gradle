// 버전 푸쉬
// ./gradlew clean shadowJar
// git tag 0.4.3
// git push origin 0.4.3

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'  // Shadow plugin 추가
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.snailcatch'
version = '0.4.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'p6spy:p6spy:3.9.1'
    implementation 'com.github.vertical-blank:sql-formatter:2.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework:spring-aop'
    implementation 'org.hibernate:hibernate-core:5.6.10.Final'
    implementation 'org.springframework:spring-context'
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation 'org.springframework:spring-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    enabled = false  // 기본 jar는 안 만듦
}

shadowJar {
    archiveBaseName.set('snailcatch-sdk-fat')
    archiveVersion.set(version)
    archiveClassifier.set('')  // classifier 없애서 기본 jar 처럼 만들기
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'snailcatch-sdk'
            artifact(tasks.shadowJar)  // shadowJar를 artifact로 지정
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sinminseok/snail-catch")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
